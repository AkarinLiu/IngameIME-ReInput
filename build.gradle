plugins {
    id "org.jetbrains.gradle.plugin.idea-ext" version "0.7"
    id "org.jetbrains.kotlin.jvm" version "1.4.21" apply false
    id "fabric-loom" version "0.5-SNAPSHOT" apply false
}

idea.project.settings {
    runConfigurations {
        "Fabric: Minecraft Client"(org.jetbrains.gradle.ext.Gradle) {
            project = rootProject.project(":fabric")
            taskNames = Collections.singletonList("runClient")
        }
        "Fabric: Minecraft Server"(org.jetbrains.gradle.ext.Gradle) {
            project = rootProject.project(":fabric")
            taskNames = Collections.singletonList("runServer")
        }
        "Forge: Minecraft Client"(org.jetbrains.gradle.ext.Gradle) {
            project = rootProject.project(":forge")
            taskNames = Collections.singletonList("runClient")
        }
        "Forge: Minecraft Server"(org.jetbrains.gradle.ext.Gradle) {
            project = rootProject.project(":forge")
            taskNames = Collections.singletonList("runServer")
        }
    }
}

subprojects{
    apply plugin: "java"
    apply plugin: "org.jetbrains.kotlin.jvm"

    group = rootProject.maven_group
    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        jcenter()
        mavenCentral()
    }
}